rule aws_s3_public_access_block_removed {
  meta:
    author = "Gowthaman"
    description = "Detect when the S3 Public Access Block configuration has been removed from a bucket or an account."
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "High"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    (
      $cloudtrail.metadata.product_event_type = "DeleteAccountPublicAccessBlock" or
      $cloudtrail.metadata.product_event_type = "DeleteBucketPublicAccessBlock" or
      (
        (
          $cloudtrail.metadata.product_event_type = "PutBucketPublicAccessBlock" or
          $cloudtrail.metadata.product_event_type = "PutAccountPublicAccessBlock"
        )
        and
        (
          $cloudtrail.target.resource.attribute.labels["RestrictPublicBuckets"] = "false" or
          $cloudtrail.target.resource.attribute.labels["BlockPublicPolicy"] = "false" or
          $cloudtrail.target.resource.attribute.labels["BlockPublicAcls"] = "false" or
          $cloudtrail.target.resource.attribute.labels["IgnorePublicAcls"] = "false"
        )
      )
    )
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.principal.user.userid = $user_id

  match:
    $user_id over 1h

  outcome:
    $risk_score = max(75)
    $mitre_attack_tactic = "Defense Evasion"
    $mitre_attack_technique = "Impair Defenses"
    $mitre_attack_technique_id = "T1562"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $target_resource_name = array_distinct($cloudtrail.target.resource.name)
    $target_resource_product_object_id = array_distinct($cloudtrail.target.resource.product_object_id)

  condition:
    $cloudtrail
}
