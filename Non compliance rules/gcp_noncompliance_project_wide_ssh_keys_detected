rule gcp_noncompliance_project_wide_ssh_keys_detected {

  meta:
    author = "Gowthaman"
    description = "Detects Google Cloud Security Command Center findings for 'Project-Wide SSH Keys Allowed', indicating a potential HIPAA 164.312(a)(2)(iv) compliance risk due to broad and less secure access control mechanisms on Compute Instances that may host ePHI."
    data_source = "Google Security Command Center"
    severity = "Medium"
    compliance = "HIPAA 164.312(a)(2)(iv), 164.312(e)(1)& 164.312(e)(2)(i)(ii); SOC2 2017 CC6.1.3, CC6.1.8, CC6.1.11, CC6.7.2"

// "Using project-wide SSH keys eases SSH key management but if compromised, poses a security risk which can impact all instances within a project. 
// It is recommended to use instance-specific SSH keys which limits the attack surface if the SSH keys are compromised. 

  events:
    $e.metadata.vendor_name = "Google"
    $e.metadata.product_name = "Security Command Center"
    $e.metadata.log_type = "GCP_SECURITYCENTER_MISCONFIGURATION"
    $e.metadata.product_event_type = "COMPUTE_PROJECT_WIDE_SSH_KEYS_ALLOWED"
    $e.security_result.alert_state = "ALERTING"
    $e.security_result.category_details = "MISCONFIGURATION"
    $e.target.resource.attribute.labels.key = "resource_type"
    $e.target.resource.attribute.labels.value = "google.compute.Instance"
    $e.target.resource.name = $resource_name


   match:
    $resource_name over 24h

   outcome :
    $risk_score = 50
    $description = "A medium-severity misconfiguration has been detected by Google Security Command Center: Project-wide SSH keys are allowed on Google Cloud Compute Instances. This indicates a potential HIPAA 164.312(a)(2)(iv) compliance risk due to broad and less secure access, particularly for environments handling ePHI."
    $event_name = array_distinct($e.metadata.product_event_type)
    $principal_resource_name = array_distinct($e.principal.resource.name)
    $target_resource_name = array_distinct($e.target.resource.name)    
    $resource_display_name = array_distinct($e.target.resource.attribute.labels["resource_displayName"])
    $resource_type = array_distinct($e.target.resource.attribute.labels["resource_type"])
    $resource_path = array_distinct($e.target.resource.attribute.labels["resource_path_string"])
    $gcp_org_id = array_distinct($e.principal.resource_ancestors.name)
    $target_resource_product_object_id = array_distinct($e.target.resource.product_object_id)
    $resource_location = array_distinct($e.target.location.name)
    $recommendation = array_distinct($e.security_result.detection_fields["sourceProperties_Recommendation"])
    $action_required_on_project = array_distinct($e.target.resource.attribute.labels["ActionRequiredOnProject"]) 

    
  condition:
    $e
}
