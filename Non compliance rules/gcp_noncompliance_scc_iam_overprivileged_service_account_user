rule gcp_noncompliance_scc_iam_overprivileged_service_account_user {
  meta:
    author = "Gowthaman"
    description = "Detects Google Security Command Center (SCC) findings related to over-privileged service account user/token creator roles granted at the project level, aligning with HIPAA control 164.312(a)(1) (Access Control - Information System Activity Review). This rule specifically targets the 'OVER_PRIVILEGED_SERVICE_ACCOUNT_USER' finding type from SCC Security Health Analytics."
    data_source = "Google Security Command Center"
    severity = "Medium"
    compliance_standard = "HIPAA164.308(a)(3)(i)(ii), 164.312(a)(1); SOC2 2017 CC5.2.3, CC6.1.3, CC6.1.7"
    

  events:
    $e.metadata.vendor_name = "Google"
    $e.metadata.product_name = "Security Command Center"
    $e.metadata.log_type = "GCP_SECURITYCENTER_MISCONFIGURATION"
    $e.metadata.product_event_type = "OVER_PRIVILEGED_SERVICE_ACCOUNT_USER"
    $e.security_result.detection_fields["state"] = "ACTIVE"
    $e.target.resource.name = $resource_name


   match:
    $resource_name over 24h

   outcome :
    $risk_score = 50
    $description = "An over-privileged Google Cloud service account has been identified within a project, surfaced by Google Security Command Center (SCC) Security Health Analytics. This flags an IAM configuration weakness that requires review for adherence to security best practices and HIPAA requirements."
    $event_name = array_distinct($e.metadata.product_event_type)
    $principal_resource_name = array_distinct($e.principal.resource.name)
    $target_resource_name = array_distinct($e.target.resource.name)    
    $resource_display_name = array_distinct($e.target.resource.attribute.labels["resource_displayName"])
    $resource_type = array_distinct($e.target.resource.attribute.labels["resource_type"])
    $resource_path = array_distinct($e.target.resource.attribute.labels["resource_path_string"])
    $gcp_org_id = array_distinct($e.principal.resource_ancestors.name)
    $target_resource_product_object_id = array_distinct($e.target.resource.product_object_id)
    $resource_location = array_distinct($e.target.location.name)
    $recommendation = array_distinct($e.security_result.detection_fields["sourceProperties_Recommendation"])
    $action_required_on_project = array_distinct($e.target.resource.attribute.labels["ActionRequiredOnProject"]) 

   
  condition:
    $e
}
