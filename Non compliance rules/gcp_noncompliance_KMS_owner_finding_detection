rule gcp_noncompliance_KMS_owner_finding_detection {

  meta:
    author = "Gowthaman"
    description = "Detects Security Command Center 'KMS_PROJECT_HAS_OWNER' findings, indicating a user has 'Owner' permissions on a GCP project containing cryptographic keys. This misconfiguation is a compliance issue as per HIPAA 164.312(a)(1) and should be addressed immediately."
    data_source = "GCP Security Command Center"
    platform = "GCP"
    severity = "Medium"
    compliance = "HIPAA 164.308(a)(3)(i)(ii), 164.312(a)(1) ; SOC2 2017 CC5.2.3, CC6.1.3, CC6.1.7"
  
  events:
	$e.metadata.vendor_name = "Google"
    $e.metadata.product_name = "Security Command Center" 
    $e.metadata.log_type = "GCP_SECURITYCENTER_MISCONFIGURATION" 
    $e.metadata.product_event_type = "KMS_PROJECT_HAS_OWNER" 
    $e.security_result.alert_state = "ALERTING" 
    $e.target.resource_ancestors.resource_type = "CLOUD_PROJECT"
    $e.target.resource.name = $resource_name
    $e.target.resource.attribute.labels["resource_projectDisplayName"] = $project
  
   match:
    $resource_name, $project over 24h

   outcome :
    $risk_score = 50
    $description = "A GCP Security Command Center finding of type KMS_PROJECT_HAS_OWNER was detected, signifying a misconfiguration where a user holds Owner permissions on a cloud project containing cryptographic keys. This directly relates to a HIPAA 164.312(a)(1) access control compliance issue."
    $event_name = array_distinct($e.metadata.product_event_type)
    $principal_resource_name = array_distinct($e.principal.resource.name)
    $target_resource_name = array_distinct($e.target.resource.name)    
    $resource_display_name = array_distinct($e.target.resource.attribute.labels["resource_displayName"])
    $resource_type = array_distinct($e.target.resource.attribute.labels["resource_type"])
    $resource_path = array_distinct($e.target.resource.attribute.labels["resource_path_string"])
    $gcp_org_id = array_distinct($e.principal.resource_ancestors.name)
    $target_resource_product_object_id = array_distinct($e.target.resource.product_object_id)
    $resource_location = array_distinct($e.target.location.name)
    $recommendation = array_distinct($e.security_result.detection_fields["sourceProperties_Recommendation"])
    $action_required_on_project = array_distinct($e.target.resource.attribute.labels["ActionRequiredOnProject"]) 


  condition:
    $e
}
