rule noncompliance_gcp_compute_public_ip {
  meta:
    author = "Gowthaman"
    description = "Detects GCE instances with public IPs violating HIPAA, CIS, NIST, and PCI standards"
    data_source = "GCP Security Command Center"
    severity = "High"
    compliance_standards = "HIPAA 164.308(a)(3)(i)(ii) & 164.312(a)(1); SOC2 2017 CC5.2.3, CC6.1.3, CC6.1.7"

  events:
    $e.metadata.vendor_name = "Google"
    $e.metadata.product_name = "Security Command Center"
    $e.metadata.log_type = "GCP_SECURITYCENTER_MISCONFIGURATION"
    $e.metadata.event_type = "GENERIC_EVENT"
    $e.metadata.product_event_type = "PUBLIC_IP_ADDRESS"
    $e.security_result.category_details = "MISCONFIGURATION"
    $e.security_result.detection_fields["state"] = "ACTIVE"
    $e.target.application = "compute.googleapis.com"
    $e.target.resource.name = $resource_name

    
  match:
    $resource_name over 24h

  outcome :
    $risk_score = 70
    $description = "A high-severity alert from GCP Security Command Center indicates a Google Compute Engine (GCE) instance with a public IP address, directly violating multiple compliance standards, including HIPAA."
    $event_name = array_distinct($e.metadata.product_event_type)
    $principal_resource_name = array_distinct($e.principal.resource.name)
    $target_resource_name = array_distinct($e.target.resource.name)    
    $resource_display_name = array_distinct($e.target.resource.attribute.labels["resource_displayName"])
    $resource_type = array_distinct($e.target.resource.attribute.labels["resource_type"])
    $resource_path = array_distinct($e.target.resource.attribute.labels["resource_path_string"])
    $gcp_org_id = array_distinct($e.principal.resource_ancestors.name)
    $target_resource_product_object_id = array_distinct($e.target.resource.product_object_id)
    $resource_location = array_distinct($e.target.location.name)
    $recommendation = array_distinct($e.security_result.detection_fields["sourceProperties_Recommendation"])
    $action_required_on_project = array_distinct($e.target.resource.attribute.labels["ActionRequiredOnProject"]) 


  condition:
    $e 
    }
