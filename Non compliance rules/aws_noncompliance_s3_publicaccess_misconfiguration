rule aws_noncompliance_s3_publicaccess_misconfiguration {
  meta:
    author = "Gowthaman"
    description = "Detects AWS S3 buckets flagged by Security Command Center for misconfigurations related to Block Public Access settings, indicating potential violations of HIPAA, NIST, and SOC2 controls for data protection and access management. Ensure that S3 Buckets are configured with 'Block public access'"
    data_source = "GCP Security Command Center"
    severity = "High"
    compliance_standard = "HIPAA 164.308(a)(3)(i)(ii) & 164.312(a)(1); SOC2 2017 CC5.2.3, CC6.1.3, CC6.1.7, NIST AC-3, AC-5, AC-6, MP-2"

  events:
    $e.metadata.vendor_name = "Google"
    $e.metadata.product_name = "Security Command Center"
    $e.metadata.log_type = "GCP_SECURITYCENTER_MISCONFIGURATION"
    $e.metadata.event_type = "GENERIC_EVENT"
    $e.metadata.product_event_type = "S3_BUCKETS_CONFIGURED_BLOCK_PUBLIC_ACCESS_BUCKET_AND_ACCOUNT_SETTINGS"
    $e.security_result.severity = "CRITICAL"
    $e.security_result.category_details = "MISCONFIGURATION"
    $e.target.resource.attribute.labels["resource_type"] = "AWS::S3::Bucket"
    $e.security_result.alert_state = "ALERTING"
    $e.security_result.detection_fields["state"] = "ACTIVE"
    $e.target.resource.name = $resource_name
    

  match:
    $resource_name over 24h

  outcome:
    $risk_score = 70
    $description = "Detection of a high-priority compliance violation: AWS S3 buckets flagged by Security Command Center for misconfigurations related to Block Public Access settings, indicating potential violations of HIPAA, NIST, and SOC2 controls for data protection and access management."
    $event_name = array_distinct($e.metadata.product_event_type)
    $principal_resource_name = array_distinct($e.principal.resource.name)
    $target_resource_name = array_distinct($e.target.resource.name)    
    $resource_display_name = array_distinct($e.target.resource.attribute.labels["resource_displayName"])
    $recipient_aws_account_id = array_distinct($e.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($e.principal.location.name)
    $target_resource_product_object_id = array_distinct($e.target.resource.product_object_id)
    $recommendation = array_distinct($e.security_result.detection_fields["sourceProperties_Recommendation"])

  condition:
    $e
}
